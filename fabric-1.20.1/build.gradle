plugins {
	id 'eclipse'
    id 'fabric-loom' version '1.+'
}

version = '1.20.3-' + modVersion
group = modGroup
base.archivesName = modName + '-fabric'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets.main.java { srcDir project(':core').sourceSets.main.java }
sourceSets.main.resources { srcDir project(':core').sourceSets.main.resources }

repositories {
	maven { url = 'https://maven.modmuss50.me/' }   // config lib
	maven { url = 'https://maven.ryanliptak.com/' } // appleskin
	maven { url = 'https://maven.shedaniel.me/' }   // cloth - needed for appleskin runtime
	maven { url = 'https://maven.terraformersmc.com/releases/' } // modmenu
	maven { url = 'https://maven.ladysnake.org/releases' } // cca
	maven { url = 'https://jitpack.io' }
	exclusiveContent {
		forRepository { maven { url = 'https://maven.parchmentmc.org' }}
		filter { includeGroup 'org.parchmentmc.data' }
	}
	exclusiveContent {
		forRepository { maven { url = 'https://api.modrinth.com/maven' }} // modrinth
		filter { includeGroup 'maven.modrinth' }
	}
	exclusiveContent {
		forRepository { maven { url = 'https://cursemaven.com' }} // curseforge
		filter { includeGroup 'curse.maven' }
	}
	flatDir {
		dir 'lib'
	}
}

dependencies {
    minecraft 'com.mojang:minecraft:1.20.3'

	mappings loom.layered() {
		officialMojangMappings()
		parchment('org.parchmentmc.data:parchment-1.20.3:2023.12.31@zip')
	}

    implementation project(':core')
    modImplementation 'net.fabricmc:fabric-loader:0.15.3'
    modImplementation 'net.fabricmc.fabric-api:fabric-api:0.90.4+1.20.2'
    implementation 'me.zeroeightsix:fiber:0.23.0-SNAPSHOT'
    include 'me.zeroeightsix:fiber:0.23.0-SNAPSHOT'

	modImplementation 'com.terraformersmc:modmenu:8.0.0'

	modCompileOnly('squeek.appleskin:appleskin-fabric:mc1.20.2-2.5.1') { transitive = false }
	modRuntimeOnly('squeek.appleskin:appleskin-fabric:mc1.20.2-2.5.1') {
		exclude module: 'fabric-api'
		exclude module: 'modmenu'
	}
	modRuntimeOnly('maven.modrinth:cloth-config:12.0.109+fabric') // required by appleskin

	modCompileOnly 'maven.modrinth:libhud:1.0.0-beta7'
	modRuntimeOnly 'maven.modrinth:libhud:1.0.0-beta7'
	modCompileOnly 'maven.modrinth:spectrum:1.6.9-1.19.2-leisure_time' // outdated
	modCompileOnly 'dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.3.0'
    modCompileOnly 'maven.modrinth:bewitchment:1.20-6'
    modCompileOnly 'maven.modrinth:dehydration:1.3.4+1.20.1'
	modCompileOnly 'maven.modrinth:better-mount-hud:1.2.2'
	modCompileOnly 'maven.modrinth:its-thirst:1.1.0-ALPHA-1.19.3' // outdated
	modCompileOnly 'maven.modrinth:detail-armor-bar:2.6.3+1.20.2-fabric'
	modCompileOnly 'maven.modrinth:victus:0.1.14+1.20.2'
}

processResources {
    inputs.property 'version', modVersion

    filesMatching('fabric.mod.json') {
        expand inputs.properties
    }
}

java {
    withSourcesJar()
}

loom {
	accessWidenerPath = file('src/main/resources/armorpointspp.accesswidener')

    runs {
		client {
			// The environment (or side) to run, usually client or server.
			environment = 'client'
			// The full name of the run configuration, i.e. 'Minecraft Client'. By default this is determined from the base name.
			configName = 'runApppClient-fabric-1.20'
			// The default main class of the run configuration. This will be overridden if using a mod loader with a fabric_installer.json file.
			defaultMainClass = ''
			// The run directory for this configuration, relative to the root project directory.
			runDir = 'run'
			// The sourceset to run, commonly set to sourceSets.test
			source = sourceSets.main
			// When true a run configuration file will be generated for IDE's. By default only set to true for the root project.
			ideConfigGenerated = true
			// vmArgs '-agentlib:jdwp=transport=dt_socket,server=n,suspend=n,address=localhost:9009'

			// Configure run config with the default client options.
			client()
		}

		debugClient {
			inherit client
			configName = 'runApppClientDebug-fabric-1.20'
			vmArgs '-agentlib:jdwp=transport=dt_socket,server=n,suspend=n,address=localhost:9009' //, '-XX:+AllowEnhancedClassRedefinition'
		}
	}
}

jar {
    manifest {
        attributes([
            'Specification-Title': 'armorpointspp',
            'Specification-Vendor': 'Cheos',
            'Specification-Version': '1', // We are version 1 of ourselves
            'Implementation-Title': project.name,
            'Implementation-Version': project.jar.archiveVersion,
            'Implementation-Vendor' : 'Cheos',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

// runDebugClient {
// 	javaLauncher = javaToolchains.launcherFor {
// 		languageVersion = JavaLanguageVersion.of(17)
// 		vendor = JvmVendorSpec.matching 'jetbrains' // fsr JvmVendorSpec.JETBRAINS doesn't work here even though it should since gradle 6.8??
// 	}
// }
