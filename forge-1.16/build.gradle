plugins {
    id 'net.minecraftforge.gradle' version '5.+'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
    id 'eclipse'
}

version = '1.16.5-' + modVersion
group = modGroup
archivesBaseName = modName + '-forge'

java.toolchain.languageVersion = JavaLanguageVersion.of(8)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'official', version: '1.16.5'
    // makeObfSourceJar = false // an Srg named sources jar is made by default. uncomment this to disable.
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')
            arg '-mixin.config=armorpointspp.mixins.json'
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            taskName 'runApppClient-1.16'

            mods {
                armorpointspp {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run')
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            taskName 'runApppServer-1.16'

            mods {
                armorpointspp {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.java { srcDir project(':core').sourceSets.main.java }
sourceSets.main.resources { srcDir project(':core').sourceSets.main.resources }

repositories {
    flatDir {
        dir 'libs'
    }

    maven {
        url 'https://www.cursemaven.com'
        content {
            includeGroup 'curse.maven'
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.16.5-36.2.39'
    implementation project(':core')

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'

    // TESTING
    // implementation fg.deobf('libs:the_vault:1.8.3p1.13.4')
    // implementation fg.deobf('curse.maven:vault-hunters-official-mod-458203:3852254')
    // implementation fg.deobf('curse.maven:tough-as-nails-246391:3430873') // ToughAsNails 1.16.5-4.1.0.9
    // implementation 'curse.maven:tough-as-nails-246391:3430874' // deobf ToughAsNails 1.16.5-4.1.0.9
    // implementation 'libs:ToughAsNails:1.16.5-4.1.0.9:deobf'
}

mixin {
    add sourceSets.main, 'armorpointspp.refmap.json'
    config 'armorpointspp.mixins.json'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "armorpointspp",
            "Specification-Vendor": "Cheos",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": project.jar.archiveVersion,
            "Implementation-Vendor" : "Cheos",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "armorpointspp.mixins.json"
        ])
    }
}

jar.finalizedBy('reobfJar')
