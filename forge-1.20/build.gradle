plugins {
    id 'net.minecraftforge.gradle' version '6.+'
    id 'org.spongepowered.mixin' version '0.7-SNAPSHOT'
    id 'eclipse'
    id 'org.parchmentmc.librarian.forgegradle' version '1.+'
}

version = '1.20-' + modVersion
group = modGroup
base.archivesName = modName + '-forge'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    mappings channel: 'parchment', version: '2023.09.03-1.20.1'
    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory 'run'
            arg '-mixin.config=armorpointspp.mixins.json'
            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            taskName 'runApppClient-1.20'

            mods {
                armorpointspp {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.java { srcDir project(':core').sourceSets.main.java }
sourceSets.main.resources { srcDir project(':core').sourceSets.main.resources }

dependencies {
    minecraft 'net.minecraftforge:forge:1.20.1-47.0.3'
    implementation project(':core')

    annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
}

mixin {
    add sourceSets.main, 'armorpointspp.refmap.json'
    config 'armorpointspp.mixins.json'
}

jar {
    manifest {
        attributes([
            "Specification-Title": "armorpointspp",
            "Specification-Vendor": "Cheos",
            "Specification-Version": "1", // We are version 1 of ourselves
            "Implementation-Title": project.name,
            "Implementation-Version": project.jar.archiveVersion,
            "Implementation-Vendor" : "Cheos",
            "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
            "MixinConfigs": "armorpointspp.mixins.json"
        ])
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}
