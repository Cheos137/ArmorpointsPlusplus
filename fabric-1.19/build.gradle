plugins {
	id 'eclipse'
    id 'fabric-loom' version '1.+'
}

version = '1.19.4-' + modVersion
group = modGroup
archivesBaseName = modName + '-fabric'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

sourceSets.main.java { srcDir project(':core').sourceSets.main.java }
sourceSets.main.resources { srcDir project(':core').sourceSets.main.resources }

repositories {
	maven { url 'https://maven.modmuss50.me/' }   // config lib
	maven { url 'https://maven.ryanliptak.com/' } // appleskin
	maven { url 'https://maven.shedaniel.me/' }   // cloth - needed for appleskin runtime
	maven { url 'https://maven.terraformersmc.com/releases/' } // modmenu
	maven { url 'https://ladysnake.jfrog.io/artifactory/mods' } // cca
	maven { url 'https://jitpack.io' }
	exclusiveContent {
		forRepository { maven { url 'https://api.modrinth.com/maven' }} // modrinth
		filter { includeGroup 'maven.modrinth' }
	}
	exclusiveContent {
		forRepository { maven { url 'https://cursemaven.com' }} // curseforge
		filter { includeGroup 'curse.maven' }
	}
	flatDir {
		dir 'lib'
	}
}

dependencies {
    minecraft 'com.mojang:minecraft:1.19.4'
	mappings loom.officialMojangMappings()
    implementation project(':core')
    modImplementation 'net.fabricmc:fabric-loader:0.14.19'
    modImplementation 'net.fabricmc.fabric-api:fabric-api:0.76.0+1.19.4'
    implementation 'me.zeroeightsix:fiber:0.23.0-SNAPSHOT'
    include 'me.zeroeightsix:fiber:0.23.0-SNAPSHOT'

	modImplementation 'com.terraformersmc:modmenu:6.1.0-rc.4'

	modCompileOnly('squeek.appleskin:appleskin-fabric:mc1.19.4-2.4.3') { transitive = false }
	modRuntimeOnly('squeek.appleskin:appleskin-fabric:mc1.19.4-2.4.3') {
		exclude module: 'fabric-api'
		exclude module: 'modmenu'
	}

	modCompileOnly 'lib:libhud:0.2.0-fabric-1.19.4'
	modCompileOnly 'maven.modrinth:spectrum:1.6.9-1.19.2-leisure_time'
	modCompileOnly 'dev.onyxstudios.cardinal-components-api:cardinal-components-base:5.1.0'
    modCompileOnly 'curse.maven:bewitchment-394915:4048571' // Bewitchment 1.19-6
}

processResources {
    inputs.property 'version', modVersion

    filesMatching('fabric.mod.json') {
        expand 'version': modVersion
    }
}

java {
    withSourcesJar()
}

loom {
	accessWidenerPath = file('src/main/resources/armorpointspp.accesswidener')

    runs {
		client {
			// The environment (or side) to run, usually client or server.
			environment = 'client'
			// The full name of the run configuration, i.e. 'Minecraft Client'. By default this is determined from the base name.
			configName = 'runApppClient-fabric-1.19'
			// The default main class of the run configuration. This will be overridden if using a mod loader with a fabric_installer.json file.
			defaultMainClass = ''
			// The run directory for this configuration, relative to the root project directory.
			runDir = 'run'
			// The sourceset to run, commonly set to sourceSets.test
			source = sourceSets.main
			// When true a run configuration file will be generated for IDE's. By default only set to true for the root project.
			ideConfigGenerated = true

			// Configure run config with the default client options.
			client()
		}
	}
}

jar {
    manifest {
        attributes([
            'Specification-Title': 'armorpointspp',
            'Specification-Vendor': 'Cheos',
            'Specification-Version': '1', // We are version 1 of ourselves
            'Implementation-Title': project.name,
            'Implementation-Version': project.jar.archiveVersion,
            'Implementation-Vendor' : 'Cheos',
            'Implementation-Timestamp': new Date().format("yyyy-MM-dd'T'HH:mm:ssZ")
        ])
    }
}
